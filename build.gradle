buildscript {
    repositories {
        maven { url = 'https://maven.minecraftforge.net' }
        maven { url = 'https://repo.spongepowered.org/maven' }
        maven { url = 'https://s01.oss.sonatype.org/content/repositories/releases/' }
        gradlePluginPortal()
        mavenCentral()
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '5.+', changing: true
        classpath group: 'org.spongepowered', name: 'mixingradle', version: '0.7-SNAPSHOT'
        classpath 'org.jetbrains.kotlin:kotlin-gradle-plugin:1.6.10'
        classpath 'org.jetbrains.kotlin:kotlin-serialization:1.6.10'
    }
}

apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'kotlin'
apply plugin: 'kotlinx-serialization'

apply plugin: 'org.spongepowered.mixin'
apply from: 'https://raw.githubusercontent.com/thedarkcolour/KotlinForForge/site/thedarkcolour/kotlinforforge/gradle/kff-3.1.0.gradle'

version = project.version
group = "possible_triangle.${project.modid}"
archivesBaseName = project.modid

java.toolchain.languageVersion = JavaLanguageVersion.of(17)

minecraft {
    mappings channel: 'official', version: project.mc_version
    accessTransformer = file('src/main/resources/META-INF/accesstransformer.cfg')

    runs {
        client {
            workingDirectory project.file('run')
            arg "-mixin.config=${project.modid}.mixins.json"

            property 'forge.logging.markers', 'SCAN,LOADING,CORE'
            property 'forge.logging.console.level', 'debug'

            mods {
                skygrid {
                    source sourceSets.main
                }
            }
        }

        server {
            workingDirectory project.file('run/server')
            arg "-mixin.config=${project.modid}.mixins.json"

            property 'forge.logging.markers', 'SCAN,LOADING,CORE'
            property 'forge.logging.console.level', 'debug'

            mods {
                skygrid {
                    source sourceSets.main
                }
            }
        }

        data {
            workingDirectory project.file('run')
            arg "-mixin.config=${project.modid}.mixins.json"

            property 'forge.logging.markers', 'SCAN,LOADING,CORE'
            property 'forge.logging.console.level', 'debug'

            args '--mod', project.modid, '--all', '--output', file('src/generated/resources/'), '--existing', file('src/main/resources')

            mods {
                skygrid {
                    source sourceSets.main
                }
            }
        }
    }
}

configurations {
    library
    implementation.extendsFrom library
    api.extendsFrom library
    library.exclude group: 'org.jetbrains', module: 'annotations'
}

minecraft.runs.all {
    lazyToken('minecraft_classpath') {
        configurations.library.copyRecursive().resolve().collect { it.absolutePath }.join(File.pathSeparator)
    }
}

// Include assets and data from data generators
sourceSets.main.resources { srcDirs 'src/generated/resources/' }

repositories {
    maven {
        // for AutoRegLib
        name "blamejared"
        url "https://maven.blamejared.com/"
    }
    maven {
        // for JEI
        name "progwml6"
        url "https://dvs1.progwml6.com/files/maven"
    }
    maven {
        url "https://cursemaven.com"
        content { includeGroup "curse.maven" }
    }
}

dependencies {
    minecraft "net.minecraftforge:forge:${project.mc_version}-${project.forge_version}"
    annotationProcessor 'org.spongepowered:mixin:0.8.5:processor'

    library "io.github.pdvrieze.xmlutil:core:0.84.0"
    library "io.github.pdvrieze.xmlutil:serialization:0.84.0"

    compileOnly fg.deobf("mezz.jei:${project.jei_version}:api")
    runtimeOnly fg.deobf("mezz.jei:${project.jei_version}")

    runtimeOnly fg.deobf("vazkii.autoreglib:AutoRegLib:${project.arl_version}")
    compileOnly fg.deobf("vazkii.autoreglib:AutoRegLib:${project.arl_version}")

    implementation fg.deobf("curse.maven:quark-243121:${project.quark_version}")
    implementation fg.deobf("curse.maven:bop-220318:${project.bop_version}")

    implementation fg.deobf("com.github.glitchfiend:TerraBlender-forge:${project.mc_version}-${project.terrablender_version}")
}

mixin {
    add sourceSets.main, "${project.modid}.refmap.json"
}

jar {
    manifest {
        attributes([
                'Specification-Title'     : project.modid,
                'Specification-Vendor'    : "examplemodsareus",
                'Specification-Version'   : "1",
                'Implementation-Title'    : project.name,
                'Implementation-Version'  : project.version,
                'Implementation-Vendor'   : "examplemodsareus",
                'Implementation-Timestamp': new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"),
                "MixinConfigs"            : "${project.modid}.mixins.json",
        ])
    }
}
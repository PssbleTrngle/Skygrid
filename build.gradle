buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'org.jetbrains.kotlin:kotlin-gradle-plugin:1.6.10'
        classpath 'org.jetbrains.kotlin:kotlin-serialization:1.6.10'
    }
}

plugins {
    id "org.sonarqube" version "3.3"
    id 'com.github.johnrengelman.shadow' version '7.1.2' apply false
    id 'com.matthewprenger.cursegradle' version '1.4.0' apply false
    id "com.modrinth.minotaur" version "2.+" apply false
}

subprojects {
    apply plugin: 'kotlin'
    apply plugin: 'kotlinx-serialization'
    apply plugin: 'com.github.johnrengelman.shadow'

    java.toolchain.languageVersion = JavaLanguageVersion.of(17)
    java.withSourcesJar()
    java.withJavadocJar()

    jar {
        manifest {
            attributes([
                    'Specification-Title'     : mod_name,
                    'Specification-Vendor'    : mod_author,
                    'Specification-Version'   : rootProject.mod_version,
                    'Implementation-Title'    : project.name,
                    'Implementation-Version'  : rootProject.mod_version,
                    'Implementation-Vendor'   : mod_author,
                    'Implementation-Timestamp': new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"),
                    'Timestampe'              : System.currentTimeMillis(),
                    'Built-On-Java'           : "${System.getProperty('java.vm.version')} (${System.getProperty('java.vm.vendor')})",
                    'Build-On-Minecraft'      : minecraft_version,
                    //"MixinConfigs": "${mod_id}.${project.name.toLowerCase()}.mixins.json"
            ])
        }
    }

    repositories {
        mavenCentral()

        maven {
            name = 'Sponge / Mixin'
            url = 'https://repo.spongepowered.org/repository/maven-public/'
        }

        maven {
            name = 'BlameJared Maven (CrT / Bookshelf)'
            url = 'https://maven.blamejared.com'
        }
    }

    tasks.withType(JavaCompile) {
        source(project(":Common").sourceSets.main.allSource)
    }

    tasks.getByName("compileKotlin") {
        source(project(":Common").sourceSets.main.allSource)
    }

    tasks.getByName("compileTestJava") {
        exclude('**/*')
    }

    tasks.withType(JavaCompile).configureEach {
        it.options.encoding = 'UTF-8'
        it.options.release = 17
    }

    // Disables Gradle's custom module metadata from being published to maven. The
    // metadata includes mapped dependencies which are not reasonably consumable by
    // other mod developers.
    tasks.withType(GenerateModuleMetadata) {
        enabled = false
    }

    configurations {
        library
        implementation.extendsFrom library
        api.extendsFrom library
        library.exclude group: 'org.jetbrains', module: 'annotations'
        //library.exclude group: 'org.jetbrains.kotlin'
        //library.exclude group: 'org.jetbrains.kotlinx'
    }

    shadowJar {
        archiveClassifier.set 'obf'
        configurations = [project.configurations.library]
        relocate 'nl.adaptivity', "${project.group}.nl.adaptivity"
        exclude 'kotlinx/**'
        exclude 'kotlin/**'

        //dependencies {
        //    include(dependency("io.github.pdvrieze.xmlutil:core:0.84.0"))
        //    include(dependency("io.github.pdvrieze.xmlutil:serialization:0.84.0"))
        //}
    }

}

sonarqube {
    properties {
        property 'sonar.projectVersion', rootProject.mod_version
        property 'sonar.projectKey', mod_id
    }
}

subprojects { project ->
    sonarqube {
        properties {
            property 'sonar.branch', project.name
        }
    }
}

task curseforge